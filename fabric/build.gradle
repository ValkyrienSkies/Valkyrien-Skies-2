plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "org.jetbrains.kotlin.jvm"
}

configurations {
    // Use shadow to shade the file but NOT add it as a dependency
    shadow
}

architectury {
    platformSetupLoomIde()
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.architect.minecraft}")
    mappings(minecraft.officialMojangMappings())
    modCompile("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")

    compileOnly(project(path: ":common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformedRuntime")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformed")) {
        transitive = false
    }

    // Depend on the fabric kotlin mod
    modImplementation("net.fabricmc:fabric-language-kotlin:1.4.21+build.1")

    // Depend on the fabric API
    modImplementation("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

    // vs-core
    implementation("com.github.ValkyrienSkies:vs-core:${rootProject.vs_core_version}") {
        exclude group: 'org.joml'
    }

    // JOML (TODO use dependency from vs-core instead)
    implementation("org.joml:joml:${rootProject.joml_version}")
    implementation("org.joml:joml-primitives:${rootProject.joml_version}")

    // Jackson Binary Dataformat for Object Serialization (TODO use dependency from vs-core instead)
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:${rootProject.jacksonVersion}")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${rootProject.jacksonVersion}")
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "fabric"
}