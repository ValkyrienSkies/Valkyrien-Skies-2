plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.jetbrains.kotlin.jvm"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")

    common(project(path: ":common", configuration: "namedElements")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }

    // Depend on the fabric kotlin mod
    modImplementation("net.fabricmc:fabric-language-kotlin:1.7.1+kotlin.1.6.10")

    // Depend on the fabric API
    modImplementation("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

    compileOnly("org.valkyrienskies.core:vs-core:${rootProject.vs_core_version}") {
        exclude module: "netty-buffer"
        exclude module: "fastutil"
        exclude module: "kotlin-stdlib-jdk8"
    }
    // Shade vs-core
    shadowCommon("org.valkyrienskies.core:vs-core:${rootProject.vs_core_version}") {
        exclude module: "netty-buffer"
        exclude module: "fastutil"
        exclude module: "kotlin-stdlib-jdk8" // Don't shade kotlin-stdlib-jdk8, even though vs-core depends on it
    }
}

// Copy the VS common access widener to the generated resources folder
//
// Note: We have to do this because fabric can't find the access widener unless its in the fabric project
File generatedResourcesDir = file("src/generated/resources")
tasks.register("copyAccessWidener", Copy) {
    from(project(":common").file("src/main/resources/valkyrienskies-common.accesswidener"))
    into(generatedResourcesDir)
}

// Add [generatedResourcesDir] as a folder to search for resources
sourceSets {
    main {
        resources {
            srcDir(generatedResourcesDir)
        }
    }
}

processResources {
    dependsOn("copyAccessWidener")
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
    dependencies {
        exclude(dependency("org.jetbrains.kotlin:.*:.*")) // Don't shade kotlin!
    }
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
}

jar {
    classifier "dev"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
}

sourcesJar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            groupId = "org.valkyrienskies"
            version = project.version
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        if (project.hasProperty('vs_maven_username') && project.hasProperty('vs_maven_url')) {
            println "Publishing to VS Maven"
            maven {
                url = project.findProperty('vs_maven_url')
                credentials {
                    username = project.findProperty('vs_maven_username')
                    password = project.findProperty('vs_maven_password')
                }
            }
        }
        if (System.getenv("GITHUB_ACTOR") != null) {
            println "Publishing to Github Packages"
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ValkyrienSkies/Valkyrien-Skies-2")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
