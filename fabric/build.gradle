plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.jetbrains.kotlin.jvm"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
}

apply from: '../gradle-scripts/publish-curseforge.gradle'

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

dependencies {
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0")))

    modImplementation("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")

    common(project(path: ":common", configuration: "namedElements")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }

    // Depend on the fabric kotlin mod
    include(modImplementation("net.fabricmc:fabric-language-kotlin:${kotlin_fabric_version}"))
    include(modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"))

    modCompileOnly("maven.modrinth:create-utilities:${create_utilities_version}")
    modImplementation("maven.modrinth:sodium:${sodium_version}")
    // Disable indium until we update sodium to newer versions
    // modRuntimeOnly("maven.modrinth:indium:${indium_version}")
    modImplementation("maven.modrinth:modmenu:${modmenu_version}")

    // Depend on the fabric API
    modImplementation("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

    modImplementation("teamreborn:energy:${energy_version}") {
        transitive = false
    }

    implementation("org.valkyrienskies.core:impl:${rootProject.vs_core_version}") {
        exclude module: "netty-buffer"
        exclude module: "fastutil"
        exclude module: "kotlin-stdlib-jdk8"
    }
    // Shade vs-core
    shadowCommon("org.valkyrienskies.core:impl:${rootProject.vs_core_version}") {
        exclude module: "netty-buffer"
        exclude module: "fastutil"
        exclude module: "kotlin-stdlib-jdk8" // Don't shade kotlin-stdlib-jdk8, even though vs-core depends on it
        exclude group: "com.google.guava"
        exclude module: "jsonschema.module.addon"
    }

    def jacksonVersion = '2.14.0'


    include("com.fasterxml:classmate:1.5.1")
    implementation("com.fasterxml:classmate:1.5.1")

    // CC Restitched
    modCompileOnly("maven.modrinth:cc-tweaked:${cc_tweaked_version}")

    // EMF compat
    modCompileOnly("curse.maven:entity-model-features-844662:5696901")
    modCompileOnly("curse.maven:entity-texture-features-fabric-568563:5697084")

    // Create compat
    modCompileOnly("com.simibubi.create:create-fabric-${minecraft_version}:${create_fabric_version}") {
        exclude group: 'com.github.AlphaMode', module: 'fakeconfigtoml'
    }
    modCompileOnly("com.jozufozu.flywheel:flywheel-fabric-${minecraft_version}:${flywheel_version_fabric}")
    modCompileOnly("com.tterrag.registrate_fabric:Registrate:${registrate_version}")

    //modImplementation("io.github.fabricators_of_create.Porting-Lib:Porting-Lib:$port_lib_version")
    for (String module in port_lib_modules.split(",")) {
        modCompileOnly("io.github.fabricators_of_create.Porting-Lib:$module:$port_lib_version")
    }

    modCompileOnly("curse.maven:forge-config-api-port-fabric-547434:$config_api_id")
    modCompileOnly("com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes_version}")
    modCompileOnly("dev.cafeteria:fake-player-api:${fake_player_api_version}")
    modCompileOnly("io.github.tropheusj:milk-lib:${milk_lib_version}")

    modCompileOnly("com.github.iPortalTeam.ImmersivePortalsMod:imm_ptl_core:${immptl_version}")
    modCompileOnly("com.github.iPortalTeam.ImmersivePortalsMod:q_misc_util:${immptl_version}")
    modCompileOnly("com.github.iPortalTeam.ImmersivePortalsMod:build:${immptl_version}")
}

// Copy the VS common access widener to the generated resources folder
//
// Note: We have to do this because fabric can't find the access widener unless its in the fabric project
File generatedResourcesDir = file("src/generated/resources")
tasks.register("copyAccessWidener", Copy) {
    from(project(":common").file("src/main/resources/valkyrienskies-common.accesswidener"))
    into(generatedResourcesDir)
}

// Add [generatedResourcesDir] as a folder to search for resources
sourceSets {
    main {
        resources {
            srcDir(generatedResourcesDir)
        }
    }
}

processResources {
    dependsOn("copyAccessWidener")
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
    dependencies {
        exclude(dependency("org.jetbrains.kotlin:.*:.*")) // Don't shade kotlin!
        exclude(dependency("org.apache.commons:commons-lang3:.*")) // Don't apache-commons, see #617
    }
    // Exclude dummy Optifine classes
    exclude "net/optifine/**"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
}

jar {
    archiveClassifier.set "dev"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
}

sourcesJar {
    dependsOn "copyAccessWidener"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE // Ignore duplicate valkyrienskies-common.accesswidener files
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

// Publish to Mavens
publishing {
    publications {
        mavenFabric(MavenPublication) {
            groupId = "org.valkyrienskies"
            version = project.version
            artifactId = rootProject.archives_base_name + "-" + project.name
            // Publish the dev shadow jar to maven
            artifact(shadowJar) {
                classifier "dev-shadow"
            }
            from components.java
        }
    }
}
