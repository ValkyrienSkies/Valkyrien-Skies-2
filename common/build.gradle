
dependencies {
    annotationProcessor(implementation("io.github.llamalad7:mixinextras-common:0.3.5"))

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    // We depend on fabric loader here to use the fabric @Environment annotations
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    modApi("me.shedaniel.cloth:cloth-config:4.14.64")

    modCompileOnly("curse.maven:sodium-394468:3669187")

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.0")

    // vs-core
    implementation("org.valkyrienskies.core:impl:${rootProject.vs_core_version}") {
        exclude module: "netty-buffer"
        exclude module: "fastutil"
        exclude module: "kotlin-stdlib"
    }

    implementation("org.valkyrienskies.core:api:${rootProject.vs_core_version}") {
        exclude module: "kotlin-stdlib"
    }
    implementation("org.valkyrienskies.core:api-game:${rootProject.vs_core_version}") {
        exclude module: "kotlin-stdlib"
    }
    implementation("org.valkyrienskies.core:util:${rootProject.vs_core_version}") {
        exclude module: "kotlin-stdlib"
    }

    // FTB Stuffs
    modCompileOnly("dev.ftb.mods:ftb-chunks-forge:1802.3.19-build.362") { transitive = false }

    // Weather2 1.18
    modCompileOnly("curse.maven:weather-storms-tornadoes-237746:4426524")

    //Common create compat,
    //We just use a version from a platform and hope the classes exist on both versions and mixins apply correctly
    modCompileOnly("com.simibubi.create:create-fabric-${minecraft_version}:${create_fabric_version}")
        { exclude group: 'com.github.AlphaMode', module: 'fakeconfigtoml'}
    modCompileOnly("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")
    modCompileOnly("com.jozufozu.flywheel:flywheel-fabric-${minecraft_version}:${flywheel_version_fabric}")
    modCompileOnly("io.github.fabricators_of_create:Porting-Lib:1.2.1451-beta+1.18.2")
    modCompileOnly("com.rbasamoyai:createbigcannons-fabric-${minecraft_version}:${createbigcannons_version}")

    //Bluemap fabric 1.18
    modCompileOnly("curse.maven:bluemap-406463:4950063")
}

architectury {
    common(rootProject.enabled_platforms.split(","))
}

loom {
    accessWidenerPath = file("src/main/resources/valkyrienskies-common.accesswidener")
}

jar {
    // Exclude dummy Optifine classes
    exclude "net/optifine/**"
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            groupId = "org.valkyrienskies"
            version = project.version
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java

            artifact(jar) {
                classifier 'dev'
            }
        }
    }
}
